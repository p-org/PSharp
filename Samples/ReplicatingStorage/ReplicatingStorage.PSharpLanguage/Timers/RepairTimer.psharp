// ------------------------------------------------------------------------------------------------
// Copyright (c) Microsoft Corporation. All rights reserved.
// Licensed under the MIT License (MIT). See License.txt in the repo root for license information.
// ------------------------------------------------------------------------------------------------

using System;

namespace ReplicatingStorage.PSharpLanguage
{
    machine RepairTimer
    {
		machine Target;

		start state Init
		{
			on RConfigureEvent do Configure;
			on StartTimer goto Active;
		}

		void Configure()
        {
            this.Target = (trigger as RConfigureEvent).Target;
            raise(StartTimer);
        }

		state Active
		{
			entry
			{
				send(this.Id, TickEvent);
			}

			on TickEvent do Tick;
			on CancelTimer goto Inactive;
			ignore StartTimer;
		}

		void Tick()
        {
            if (*)
            {
                this.Logger.WriteLine("\n [RepairTimer] " + this.Target + " | timed out\n");
                send(this.Target, Timeout);
            }

            send(this.Id, TickEvent);
        }

		state Inactive
		{
			on StartTimer goto Active;
			ignore CancelTimer, TickEvent;
		}
    }
}
